traqula = _{
    SOI ~
    (add_role | add_posit)* ~
    EOI
}

// common tokens
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
digit = _{ '0'..'9' }
int = { "-"? ~ digit+ }
decimal = { "-"? ~ digit+ ~ "." ~ digit+ }
certainty = { "-"? ~ digit{1,3} ~ "%" }
time = { "'" ~ (digit | "-" | ":")+ ~ "'" }
json = @{ "{" ~ (json | !"}" ~ ANY)* ~ "}" }
string = { "\"" ~ ( "\"\"" | (!"\"" ~ ANY) )* ~ "\"" }
variable = { CASED_LETTER ~ (CASED_LETTER | digit | "_" )+ }

// add role command
role = { (CASED_LETTER | SPACE_SEPARATOR)+ }
add_role = { ^"add role " ~ (role ~ ","?)+ ~ ";" }

// add posit command
generate = { "+" ~ variable }
recollect = { "$" ~ variable }
appearance = { "(" ~ (generate | recollect) ~ "," ~ role ~ ")" } 
appearance_set = { "{" ~ (appearance ~ ","?)+ ~ "}" }
appearing_value = { json | string | time | certainty | decimal | int }
appearance_time = { time }
posit = { "[" ~ appearance_set ~ "," ~ appearing_value ~ "," ~ appearance_time ~ "]" }
add_posit = { ^"add posit " ~ (generate? ~ posit ~ ","?)+ ~ ";" } 
