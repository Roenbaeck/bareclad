add role  wife, husband, name;

add posit [{(+idw, wife), (+idh, husband)}, "married", '2004-06-19'], /* first married */
          [{(idw, wife), (idh, husband)}, "divorced", '2020-12-04'], /* then divorced */
          [{(idw, wife), (idh, husband)}, "married", '2024-03-17'], /* then remarried */
          [{(idh, name)}, "Archie Bald", '1972-08-20'], /* husband's given name at birth */
          [{(idh, name)}, "Archie Trix", '2004-09-21'], /* took wife's surname after marriage */
          [{(idh, name)}, "Archie Bald", '2021-01-19'], /* reverted to birth name after divorce */
          [{(idw, name)}, "Bella Trix", '1972-12-13'], /* wife's given name at birth */
          [{(idw, name)}, "Bella Bald", '2024-05-29']; /* took husband's surname after remarrying him */

/* sanity check, give me all names stored for divorced couples (at any point in time) */
search    [{(+w, wife), (+h, husband)}, "divorced", *], 
          [{(w|h, name)}, +n, +t]
return
    n, t;

/* give me all names stored for couples that are currently divorced */
search    [{(+w, wife), (+h, husband)}, "divorced", +mt] as of @NOW, 
          [{(w|h, name)}, +n, +t]
return
    n, t, mt;

/* give me all names stored for couples that are currently married */
search    [{(+w, wife), (+h, husband)}, "married", +mt] as of @NOW, 
          [{(w|h, name)}, +n, +t]
return
    n, t, mt;

/* give me the name of each wife and husband at the time they got married */
search    [{(+w, wife), (+h, husband)}, "married", +mt] as of @NOW, 
          [{(w|h, name)}, +n, +t] as of mt
return
    n, t, mt;

/* give me the name history of each wife and husband up to and including the time of their marriage */
search    [{(+w, wife), (+h, husband)}, "married", +mt] as of @NOW, 
          [{(w|h, name)}, +n, +t] 
where t <= mt
return
    n, t, mt;

/* give me the name of each wife and husband that were divorced before 2022 */
search    [{(+w, wife), (+h, husband)}, "divorced", +mt] as of '2022', 
          [{(w|h, name)}, +n, +t] as of mt
return
    n, t, mt;

/* give me the name history of each wife and husband up to and including the time of their divorce */
search    [{(+w, wife), (+h, husband)}, "married", +mt] as of '2022', 
          [{(w|h, name)}, +n, +t] 
where t <= mt
return
    n, t, mt;
